grammar org.github.FreemarkerEditor

generate freemarkerEditor "http://www.github.org/FreemarkerEditor"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Freemarker:
	elements+=Element*;
	
Element: Print|Comment|Tag|Text;

Print:PRINT_START expression=PrintContent PRINT_END;
PrintContent returns ecore::EString:(CHAR|COMMENT_OPEN|COMMENT_CLOSE|TAG_OPEN_START|TAG_CLOSE_START|PRINT_START|TAG_END)+;

Tag: openTag=OpenTag elements+=Element* closeTag=CloseTag;
TagPart:OpenTag|CloseTag;
OpenTag:TAG_OPEN_START content=TagContent TAG_END;
CloseTag:TAG_CLOSE_START content=TagContent  TAG_END;
TagContent returns ecore::EString:(CHAR|COMMENT_OPEN|COMMENT_CLOSE|TAG_OPEN_START|TAG_CLOSE_START|PRINT_START|PRINT_END)+;

Text: text=TextContent;
TextContent returns ecore::EString:=> (CHAR|COMMENT_CLOSE|PRINT_END|TAG_END)+;

Comment: {Comment}COMMENT_OPEN text=CommentContent COMMENT_CLOSE;
CommentContent returns ecore::EString:(CHAR|COMMENT_OPEN|TAG_OPEN_START|TAG_CLOSE_START|PRINT_START|PRINT_END|TAG_END)*;

terminal COMMENT_OPEN returns ecore::EString:'<#--';
terminal COMMENT_CLOSE returns ecore::EString:'-->';
terminal TAG_OPEN_START returns ecore::EString:'<#';
terminal TAG_CLOSE_START returns ecore::EString:'</#';
terminal TAG_END returns ecore::EString:'>';
terminal PRINT_START returns ecore::EString:'${';
terminal PRINT_END returns ecore::EString:'}';
terminal CHAR returns ecore::EString:'--'|'</'|!(COMMENT_OPEN|COMMENT_CLOSE|TAG_OPEN_START|TAG_CLOSE_START|PRINT_START|PRINT_END|TAG_END);